plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'me.dio'
version = '0.0.1-SNAPSHOT'
description = 'Java RESTful API criada durante o Bootcamp Santander'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
}

tasks.jar {
    manifest {
        attributes["Main-Class"] = "me.dio.santander_bootcamp.Application"
    }
}

tasks.named('test') {
	useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // Executa testes antes de gerar relatório

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    // Configuração para ignorar classes que não precisam de cobertura
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/Application.class',
                    '**/dto/**',
                    '**/config/**',
                    '**/exception/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.70 // Mínimo de 70% de cobertura
            }
        }

        rule {
            element = 'CLASS'
            excludes = [
                    '*.Application',
                    '*.dto.*',
                    '*.config.*',
                    '*.exception.*'
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
        }
    }
}

// Tarefa para verificar cobertura (falha build se não atingir mínimo)
check {
    dependsOn jacocoTestCoverageVerification
}
